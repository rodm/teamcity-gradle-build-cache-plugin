
plugins {
    id 'org.gradle.java'
    id 'org.gradle.groovy'
    id 'org.gradle.jacoco'
    id 'com.github.rodm.teamcity-server'
    id 'com.github.rodm.teamcity-environments'
}

dependencies {
    implementation project(':common')
    implementation group: 'com.hazelcast', name: 'hazelcast', version: '3.8'

    agent (project(path: ':agent', configuration: 'plugin'))

    testImplementation localGroovy()
    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

teamcity {
    server {
        archiveName = "gradle-build-cache-${rootProject.version}.zip"

        descriptor {
            name = 'gradleBuildCache'
            displayName = 'Gradle Build Cache'
            version = rootProject.version
            description = 'TeamCity plugin to enable the server to act as a remote Gradle Build Cache'
            vendorName = 'Rod MacKenzie'
            vendorUrl = 'https://github.com/rodm'
            email = 'rod.n.mackenzie@gmail.com'
            downloadUrl = 'https://github.com/rodm/teamcity-gradle-build-cache-plugin'
            useSeparateClassloader = true
        }

        publish {
            token = findProperty('jetbrains.token')
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = "${rootDir}/data"

        'teamcity2018.1' {
            version = '2018.1.5'
            javaHome = file(java8Home)
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
            agentOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006'
        }

        'teamcity2018.2' {
            version = '2018.2.4'
            javaHome = file(java8Home)
        }
    }
}
